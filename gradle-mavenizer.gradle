apply plugin: 'maven-publish'

def gradlePluginVersion = project.hasProperty('mavPluginVersion') ? project.mavPluginVersion : 'master'
apply from: "https://raw.githubusercontent.com/chillbrodev/gradle-maven-plugin/${gradlePluginVersion}/utils.gradle"
apply from: "https://raw.githubusercontent.com/chillbrodev/gradle-maven-plugin/${gradlePluginVersion}/artifact-javadoc-handler.gradle"
apply from: "https://raw.githubusercontent.com/chillbrodev/gradle-maven-plugin/${gradlePluginVersion}/artifact-pom-manager.gradle"

project.afterEvaluate {
    publishing {
        publications {
            mavenPublish(MavenPublication) {
                groupId getStringProperty("mavArtifactGroupId")
                artifactId getStringProperty("mavArtifactArtifactId")
                version getStringProperty("mavArtifactVersion")
                if (isAndroidProject()) {
                    artifact bundleReleaseAar
                    artifact androidJavadocsJar
                    artifact androidSourcesJar
                } else {
                    artifact jar
                    artifact sourcesJar
                    artifact javadocJar
                }
                
                decoratePom(pom)
            }
        }
        
        repositories {
            def isToRemoteRepo = getBooleanProperty("mavPublishToRemoteRepo")
            def isToInternalRepo = getBooleanProperty("mavPublishToInternalRepo")
            def isToMavenLocal = getBooleanProperty("mavPublishToMavenLocal")
            def remoteRepoName = getStringProperty("mavRepoRemoteName")

        
            if (isToRemoteRepo) {
                if(remoteRepoName.isEmpty()) {
                    throw new GradleException('Unable to perform remote upload. Missing Remote Repo Name. Set with property: mavRepoRemoteName')
                }
                maven {
                    name = remoteRepoName
                    credentials {
                        username getStringProperty("mavRemoteRepoUser")
                        password getStringProperty("mavRemoteRepoPassword")
                    }
                    url = getStringProperty("mavRepoRemoteUrl")
                }
            }

            if (isToInternalRepo) {
                maven {
                    name = remoteRepoName
                    url = getStringProperty("mavRepoInternalUrl")
                }
            }

            if (isToMavenLocal || (!isToInternalRepo && !isToRemoteRepo)) {
                mavenLocal()
            }
        }
    }
}
